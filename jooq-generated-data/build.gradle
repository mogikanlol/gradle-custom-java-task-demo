plugins {
    id ('java')
}

sourceSets {
    jooq {
        java {
            srcDir file('src/jooq/java')
        }
    }
}

task runApp(type: JavaExec) {
    classpath = sourceSets.jooq.runtimeClasspath

    mainClass = 'internal.Main'
    dependsOn(":liquibase-migrations:build")

    logging.captureStandardOutput LogLevel.INFO

    outputs.dir('src/main/java/')
}

compileJava {
    dependsOn runApp
}

clean {
    delete('src/main')
}

dependencies {
    jooqImplementation project(":liquibase-migrations")

    jooqImplementation('org.jooq:jooq-codegen:3.15.1')
    jooqImplementation('org.testcontainers:postgresql:1.16.2')
    jooqImplementation('org.liquibase:liquibase-core:3.8.8')
    jooqImplementation('org.postgresql:postgresql:42.3.1')
    jooqImplementation group: 'org.yaml', name: 'snakeyaml', version: '1.29'

    implementation('org.jooq:jooq:3.15.1')

}
